open Lang
open Xl
open Il
module Value = Runtime.Dynamic_Il.Value
open Util.Source

(* dec $text_to_int(text) : int *)

let text_to_int (add : value -> unit) (at : region) (targs : targ list)
    (values_input : value list) : value =
  Extract.zero at targs;
  let text = Extract.one at values_input |> Value.get_text in
  let i = Bigint.of_string text in
  let value = Value.make (Il.NumT `IntT) (NumV (`Int i)) in
  add value;
  value

(* dec $int_to_text(int) : text *)

let int_to_text (add : value -> unit) (at : region) (targs : targ list)
    (values_input : value list) : value =
  Extract.zero at targs;
  let num = Extract.one at values_input |> Value.get_num in
  let value = Value.make Il.TextT (TextV (Num.string_of_num num)) in
  add value;
  value

(* dec $split_text(text, text) : text* *)

let split_text (add : value -> unit) (at : region) (targs : targ list)
    (values_input : value list) : value =
  Extract.zero at targs;
  let value_text, value_separator = Extract.two at values_input in
  let text = Value.get_text value_text in
  let separator = Value.get_text value_separator in
  assert (String.length separator = 1);
  let parts = String.split_on_char (String.get separator 0) text in
  let values = List.map (fun part -> Value.make Il.TextT (TextV part)) parts in
  let value =
    let typ = Il.IterT (Il.TextT $ no_region, Il.List) in
    Value.make typ (ListV values)
  in
  add value;
  value

(* dec $strip_prefix(text, text) : text *)

let strip_prefix (add : value -> unit) (at : region) (targs : targ list)
    (values_input : value list) : value =
  Extract.zero at targs;
  let value_text, value_prefix = Extract.two at values_input in
  let text = Value.get_text value_text in
  let prefix = Value.get_text value_prefix in
  assert (String.starts_with ~prefix text);
  let text =
    String.sub text (String.length prefix)
      (String.length text - String.length prefix)
  in
  let value = Value.make Il.TextT (TextV text) in
  add value;
  value

(* dec $strip_suffix(text, text) : text *)

let strip_suffix (add : value -> unit) (at : region) (targs : targ list)
    (values_input : value list) : value =
  Extract.zero at targs;
  let value_text, value_suffix = Extract.two at values_input in
  let text = Value.get_text value_text in
  let suffix = Value.get_text value_suffix in
  assert (String.ends_with ~suffix text);
  let text = String.sub text 0 (String.length text - String.length suffix) in
  let value = Value.make Il.TextT (TextV text) in
  add value;
  value

(* dec $strip_all_whitespace(text) : text *)

let strip_all_whitespace (add : value -> unit) (at : region) (targs : targ list)
    (values_input : value list) : value =
  Extract.zero at targs;
  let value = Extract.one at values_input in
  let text =
    value |> Value.get_text |> String.split_on_char ' ' |> String.concat ""
  in
  let value = Value.make Il.TextT (TextV text) in
  add value;
  value
